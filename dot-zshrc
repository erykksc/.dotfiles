#!/usr/bin/env zsh

# Load secrets
[ -f ~/.secrets ] && source ~/.secrets

# ALIASES
alias v='nvim'
alias vc='nvim .'
alias gs='git status'
alias ls='ls --color=auto -p -h'
alias s='~/.dotfiles/scripts/tmux-client.sh'

# Goland tools - use go tools if available, fallback to global commands
golangci-lint() {
  if go tool golangci-lint version >/dev/null 2>&1; then
    go tool golangci-lint "$@"
  else
    command golangci-lint "$@"
  fi
}

goimports() {
  if go tool goimports --help >/dev/null 2>&1; then
    go tool goimports "$@"
  else
    command goimports "$@"
  fi
}

# KEYBINDINGS
bindkey -e
# Move through history with the same prefix
bindkey '^P' history-search-backward
bindkey '^N' history-search-forward

# HISTORY
HISTSIZE=100000
HISTFILE=~/.zsh_history
SAVEHIST=$HISTSIZE
HISTDUP=erase
setopt APPEND_HISTORY
setopt INC_APPEND_HISTORY
setopt SHARE_HISTORY
setopt HIST_IGNORE_ALL_DUPS
setopt HIST_IGNORE_SPACE

setopt EXTENDED_GLOB

# PROMPT
REPORTTIME=5 # show how much time did a function take when it run for more than 5s
PROMPT_HOST="%F{yellow}%n@%m%f"
autoload -Uz vcs_info
precmd() {
  print "" # add empty line between prompts
  vcs_info
} 
setopt prompt_subst
zstyle ':vcs_info:git*' formats "%b"
PROMPT='${VIRTUAL_ENV_PROMPT:+($VIRTUAL_ENV_PROMPT) }${PROMPT_HOST} %F{green}%~%F{magenta}${vcs_info_msg_0_:+ ($vcs_info_msg_0_)}%F{blue}${IN_NIX_SHELL:+ nix-shell} %F{red}%(?..[exit:%?])%f
%# '

# FZF
# Set up fzf key bindings and fuzzy completion
if command -v fzf > /dev/null 2>&1; then
  source <(fzf --zsh)
fi

# Enable completions
fpath=(
  /opt/homebrew/share/zsh/site-functions
  /usr/share/zsh/site-functions
  /usr/local/share/zsh/site-functions
  $fpath
)
autoload -Uz compinit
compinit

if command -v direnv > /dev/null 2>&1; then
  eval "$(direnv hook zsh)"
fi

# PLUGINS
ZPLUGINDIR=${XDG_DATA_HOME:-$HOME/.local/share}/zsh-plugins
mkdir -p $ZPLUGINDIR

# zsh-autosuggestions
if [[ ! -d $ZPLUGINDIR/zsh-autosuggestions ]]; then
  git clone https://github.com/zsh-users/zsh-autosuggestions \
    $ZPLUGINDIR/zsh-autosuggestions
fi
source $ZPLUGINDIR/zsh-autosuggestions/zsh-autosuggestions.plugin.zsh

# zsh-syntax-highlighting
if [[ ! -d $ZPLUGINDIR/zsh-syntax-highlighting ]]; then
  git clone https://github.com/zsh-users/zsh-syntax-highlighting \
    $ZPLUGINDIR/zsh-syntax-highlighting
fi
source $ZPLUGINDIR/zsh-syntax-highlighting/zsh-syntax-highlighting.plugin.zsh
